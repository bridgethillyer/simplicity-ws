(page "charsheet.html"
      (:require [tailrecursion.hoplon.reload :refer [reload-all]]))

(reload-all)

(def classes
  {"fighter"    {:thac0 [0 1 1 2 3 4 5 6 6 7 8 8 9 10 11 12 13 14 15 16]
                 :weapons ["fists" "dagger" "hand-axe" "mace" "sword" "warhammer" "greatsword"]
                 :armor ["none" "leather" "scale" "chain" "plate"]}
   "cleric"     {:thac0 [0 1 1 1 2 2 3 3 3 4 4 5 6  7  7  8  8  9  9 10 10 11]
                 :weapons ["fists" "mace" "warhammer"]
                 :armor ["none" "leather" "scale" "chain" "plate"]}
   "magic-user" {:thac0 [0 1 1 1 2 2 2 2 3 3 3 4 4  5  6  6  7  7  7  8  8  9  9  9  8]
                 :weapons ["fists" "dagger"]
                 :armor ["none"]}
   "thief"      {:thac0 [0 1 1 1 2 2 3 3 3 4 4 5 6  7  7  8  8  9  9 10 10 11]
                 :weapons ["fists" "dagger" "hand-axe" "mace" "sword"]
                 :armor ["none" "leather"]}})

(def armors
  {"none" 9
   "leather" 7
   "scale" 6
   "chain" 5
   "plate" 3})

(def weapons
  {"fists" 3
   "dagger" 4
   "hand-axe" 6
   "mace" 6
   "sword" 8
   "warhammer" 8
   "greatsword" 10})

(defn attr-bonus
  [attr]
  (cond
   (<= attr 3) -3
   (<= attr 5) -2
   (<= attr 8) -1
   (<= attr 12) 0
   (<= attr 15) 1
   (<= attr 17) 2
   :else 3))

(defn format-bonus
  [bonus]
  (if (>= bonus 0)
    (str "+" bonus)
    (str bonus)))

(defn format-weapon-damage
  [die-size bonus]
  
  (str "1d" die-size
       (cond
        (> bonus 0) (str " + " bonus)
        (< bonus 0) (str " - " (- 0 bonus))
        :else "")))

;;; CELLS

(defc level 1)
(defc class "fighter")
(defc weapon "fists")
(defc armor "none")
(defc attrs
  {:str 10
   :dex 10
   :con 10
   :int 10
   :wis 10
   :cha 10})

(defc= attr-bonuses
  (into {} (map (fn [[key value]]
                  [key (attr-bonus value)]) attrs)))

(defc= thac0
  (let [table (get-in classes [class :thac0])]
    (- 20 (get table level (last table)))))

(defc= armor-class
  (let [base-ac (get armors armor)]
    (- base-ac (:dex attr-bonuses))))

(defc= weapon-damage
  (get weapons weapon))

(defc= weapon-damage-text
  (format-weapon-damage weapon-damage (attr-bonuses :str)))

(defn attr-input
  [attr]
  [(td (input :id attr :type "text" :value (cell= (attr attrs)) :input #(swap! attrs assoc attr (int @%))))
   (td (span :class "bonus" (text "~(format-bonus (attr-bonuses attr))")))])

(html
  (head
   (link :rel "stylesheet" :type "text/css" :href "normalize.css")
   (link :rel "stylesheet" :type "text/css" :href "foundation.min.css")
   (link :rel "stylesheet" :type "text/css" :href "charsheet.css")
   (link :rel "stylesheet" :type "text/css" :href "http://fonts.googleapis.com/css?family=PT+Serif:400,700|Germania+One"))
  (body
   (div :class "row" :style "margin-top: 30px"
        (h1 :class "large-12 columns text-center"
            "Dungeons & Dragons")
        (div :class "large-12 columns"
             (table
              :class "large-12 columns"
              (tr
               (th (label :for "level" :class "inline right" "Level"))
               (td (input :id "level" :type "text" :value level :change #(reset! level (int @%))))
               (th (label :for "class" :class "inline right" "Class"))
               (td (select
                    :id "class"
                    :value class
                    :change #(reset! class @%)
                    (option :value "fighter" "Fighter")
                    (option :value "cleric" "Cleric")
                    (option :value "magic-user" "Magic-User")
                    (option :value "thief" "Thief"))))))
        )
   (div :class "row"
        (div :class "large-6 columns"
             (table
              :class "large-12 columns"
              (tr
               (th (label :for "str" :class "inline right" "Str"))
               (attr-input :str))
              (tr
               (th (label :for "dex" :class "inline right" "Dex"))
               (attr-input :dex))
              (tr
               (th (label :for "con" :class "inline right" "Con"))
               (attr-input :con))
              (tr
               (th (label :for "int" :class "inline right" "Int"))
               (attr-input :int))
              (tr
               (th (label :for "wis" :class "inline right" "Wis"))
               (attr-input :wis))
              (tr
               (th (label :for "cha" :class "inline right" "Cha"))
               (attr-input :cha))))

        (div :class "large-6 columns"
         (table
          :class "large-12 columns"
          (tr
           (th (label :for "weapon" :class "inline right" "Weapon"))
           (td (select
                :id "weapon"
                :value weapon
                :change #(reset! weapon @%)
                (loop-tpl :bindings [weapon (cell= (get-in classes [class :weapons]))]
                          (option weapon)))))
          (tr
           (th (label :for "armor" :class "inline right" "Armor"))
           (td (select
                :id "armor"
                :value armor
                :change #(reset! armor @%)
                (loop-tpl :bindings [armor (cell= (get-in classes [class :armor]))]
                          (option armor)))))
          (tr
           (td (span :class "right" "THAC0 (melee)"))
           (td (text "~(- thac0 (attr-bonuses :str))")))
          (tr
           (td (span :class "right" "THAC0 (missile)"))
           (td (text "~(- thac0 (attr-bonuses :dex))")))
          (tr
           (td (span :class "right" "Armor Class"))
           (td (text "~{armor-class}")))
          (tr
           (td (span :class "right" (text "Damage (~{weapon})")))
           (td (text "~{weapon-damage-text}"))))))))
